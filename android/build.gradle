plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
    id 'org.jetbrains.dokka'
}

def ver_major = 0
def ver_minor = 0
def ver_revision = 1
version = "${ver_major}.${ver_minor}.${ver_revision}"

android {
    buildTypes {
        release {
            minifyEnabled false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    compileSdk 31
    defaultConfig {
        consumerProguardFiles "consumer-rules.pro"
        minSdk 21
        targetSdk 31
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        versionCode 10000 * ver_major + 100 * ver_minor + ver_revision
        versionName version
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    // AndroidX
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.core:core-ktx:1.7.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    implementation 'com.google.android.material:material:1.5.0'

    // JUnit
    testImplementation 'junit:junit:4.+'

    // OkHttp
    implementation platform("com.squareup.okhttp3:okhttp-bom:4.9.0")
    implementation "com.squareup.okhttp3:okhttp"
}


afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                artifact javadocJar {
                    classifier = 'javadoc'
                    extension = 'jar'
                }
                artifact sourcesJar

                artifactId = 'mktools_android'
                groupId = 'com.github.tshion'
                version = this.version
            }
        }
    }
}


task javadocJar(type: Jar, dependsOn: "dokkaJavadoc") {
    archiveClassifier.set("javadoc")
    from dokkaJavadoc.outputDirectory
}

task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from android.sourceSets.main.java.srcDirs
}
